plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.0'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'kr.entree.spigradle' version '1.2.1'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.72'
    id 'maven-publish'
    id 'org.jetbrains.dokka' version '1.4.0-rc'
}

group = pluginGroup
version = pluginVersion

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    mavenLocal()
    bungeecord()
    jitpack()
    jcenter()
    codemc()
    maven {
        name 'acf'
        url 'https://repo.aikar.co/content/groups/aikar/'
    }
    maven {
        name 'papi'
        url 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    compileOnly spigot('1.16.2-R0.1-SNAPSHOT')

    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-cbor:1.0.0-RC'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
    implementation 'de.tr7zw:item-nbt-api-plugin:2.5.0'

    implementation "me.mattstudios.utils:matt-framework-gui:2.0.2"
    //Source: https://github.com/knightzmc/mf-gui-kotlin
    implementation "me.bristermitten:mf-gui-kotlin:1.0-SNAPSHOT"

    compileOnly 'me.clip:placeholderapi:2.10.9'

    testImplementation 'junit:junit:4.12'
    testCompileOnly spigot('1.16.2-R0.1-SNAPSHOT')
    testImplementation('com.github.knightzmc:MockBukkit:ecaf5f7462bbd18869d3198c1ced6766eeae93e0') {
        exclude group: 'org.spigotmc'
    }
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

shadowJar {
    minimize()
    relocate 'kotlin', 'me.bristermitten.backpacks.libs.kotlin'
    relocate 'kotlinx', 'me.bristermitten.backpacks.libs.kotlinx'
    relocate 'co.aikar.commands', 'me.bristermitten.backpacks.libs.acf'
    relocate 'co.aikar.locales', 'me.bristermitten.backpacks.libs.acf-locales'
}


compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.javaParameters = true
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

spigot {
    apiVersion '1.16'
    authors = ['BristerMitten']
    description 'A Simple and Effective Backpacks plugin.'
    website 'https://bristermitten.me'
    softDepends = ['PlaceholderAPI']
}

dokkaHtml {
    outputDirectory = "$buildDir/dokka"
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'backpacks'
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = "https://repo.bristermitten.me/repository/maven-releases"
            def snapshotsRepoUrl = "https://repo.bristermitten.me/repository/maven-snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username project.hasProperty('mavenUser') ? mavenUser : System.getenv("MAVEN_USER")
                password project.hasProperty('mavenPassword') ? mavenPassword : System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}
